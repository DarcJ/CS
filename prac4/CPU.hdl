// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    //MAIN MODULES 
    ALU(x=outD, y=outAM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out = outALU, out=outM, zr=zr, ng=ng);
    ARegister(in=inA, load=loadA, out=outA, out[0..14] = addressM); //Register A
    DRegister(in=outALU , load=loadD, out=outD); //Register D
    PC(in=outA, load=loadPC, inc=true, reset=reset, out[0..14]=pc);

    //Intermediate Modules
    Mux16(a=outALU, b=instruction, sel=instA, out=inA);
    Mux16(a=outA, b=inM, sel=AMtoALU, out=outAM);

    //Decoder
        //Instruction A Logic
            Not(in=instruction[15], out=instA);
        //LoadD logic
            And(a=instruction[15], b=instruction[4], out=loadD);
        //LoadA logic
        And(a=instC, b=instruction[5], out=destA);
            Or(a=instA, b=destA, out=loadA);
        //loadPC
            Not(in=instA, out=instC); // instruction is a C instruction 
            Or(a=zr, b=ng, out=orO);
            Not(in=orO, out=pos); //Positive Output
            //JDT insturction
            And(a=instruction[0], b=pos, out=JGT); //Jump when x -y is positive
            //JEQ instruction
            And(a=instruction[1], b=zr, out=JEQ); //Jump when x - y equals zero
            //JLT
            And(a=instruction[2], b=ng, out=JLT); //Jump when x - y is a negative value
            //JGE
            Or(a=JGT, b=JEQ, out=JGE);
            //JNE
            Or(a=JLT, b=JGT, out=JNE);
            //JLE
            Or(a=JLT, b=JEQ, out=JLE);
            //JMP
            Or(a=JGT, b=JLE, out=JMP); 

           Or(a=JGT, b=JEQ, out=o1);
           Or(a=JLT, b=o1, out=o2);
           Or(a=JGE, b=o2, out=o3);
           Or(a=JNE, b=o3, out=o4);
           Or(a=JLE, b=o4, out=o5);
           Or(a=JMP, b=o5, out=o6);
           And(a=instC, b=o6, out=loadPC);
        //AM to ALU insturction
        And(a=instC, b=instruction[12], out=AMtoALU);
        //loadM logic
        And(a=instC, b = instruction[3], out = loadM, out =writeM);   

}